import org.apache.tools.ant.taskdefs.condition.Os

def reactConfig = project.hasProperty("react") ? project.react : [:]
def reactRoot = file(reactConfig.root ?: "../../")

def datadogConfig = new Properties()
def datadogConfigFile = new File(reactRoot, ".datadog.sourcemaps.env")

if (datadogConfigFile.exists()) {
    datadogConfigFile.withInputStream {
        datadogConfig.load(it)
    }
}

afterEvaluate {

    def androidExtension = extensions.findByName("android")

    if (androidExtension == null || android.applicationVariants == null) {
        throw new GradleException("Android extension is missing or running not in the application" +
                " module. Make sure you've applied datadog-sourcemaps.gradle in the" +
                " application module (usually it is located in the android/app/build.gradle file).")
    }

    androidExtension.applicationVariants.all { variant ->

        // TODO service name per variant?
        def serviceName = datadogConfig.SERVICE_NAME_ANDROID ?: variant.applicationId

        logger.info("Service name used for the upload is ${serviceName}.")

        def releaseVersion = variant.versionName
        def buildVersion = variant.versionCode

        if (releaseVersion == null) {
            throw new GradleException("Cannot determine application version. Make sure to" +
                    " define versionName property in the application/variant config.")
        }

        // locations and names are defined in "../../node_modules/react-native/react.gradle"
        def targetName = variant.name.capitalize()
        def targetPath = variant.dirName

        def bundleTask = tasks.findByName("bundle${targetName}JsAndAssets")

        if (bundleTask == null) {
            logger.info("Cannot find JS bundle task for target=${targetName}.")
            return
        }
        if (!bundleTask.enabled) {
            logger.info("JS bundle task for target=${targetName} is not enabled.")
            return
        }

        def bundleAssetName = reactConfig.bundleAssetName ?: "index.android.bundle"

        def jsBundleDir = file("$buildDir/generated/assets/react/${targetPath}")
        def jsBundleFile = file("$jsBundleDir/$bundleAssetName")

        def jsSourceMapsDir = file("$buildDir/generated/sourcemaps/react/${targetPath}")
        def jsOutputSourceMapFile = file("$jsSourceMapsDir/${bundleAssetName}.map")

        def uploadTask = tasks.create("upload${targetName}Sourcemaps") {
            group = "datadog"
            description = "Uploads sourcemaps to Datadog."

            def execCommand = [
                    "yarn",
                    "datadog-ci",
                    "react-native",
                    "upload",
                    "--platform",
                    "android",
                    "--service",
                    serviceName,
                    "--bundle",
                    jsBundleFile.absolutePath,
                    "--sourcemap",
                    jsOutputSourceMapFile.absolutePath,
                    "--release-version",
                    releaseVersion,
                    "--build-version",
                    buildVersion
            ]

            doFirst {
                if (!jsBundleFile.exists()) {
                    throw new GradleException("JS bundle file doesn't exist, aborting upload.")
                }

                if (!jsOutputSourceMapFile.exists()) {
                    throw new GradleException("JS sourcemap file doesn't exist, aborting upload.")
                }

                def env = [:]
                if (System.getenv("DATADOG_API_KEY") == null) {
                    if (datadogConfig.DATADOG_API_KEY == null) {
                        throw new GradleException("Cannot find Datadog API key, it is neither" +
                                " in env, nor in .datadog.sourcemaps.env file.")
                    }
                    env["DATADOG_API_KEY"] = datadogConfig.DATADOG_API_KEY
                }

                logger.info("Going to run sourcemaps upload using the following command:" +
                        " ${execCommand.join(" ")} and env=${env}")
                runShellCommand(execCommand, reactRoot, env)

            }
        }

        uploadTask.dependsOn bundleTask
        bundleTask.finalizedBy uploadTask

    }
}

def runShellCommand(List<String> command, File workingDirectory, Map<String, String> env = [:]) {
    def outputStream = new ByteArrayOutputStream()
    def errorStream = new ByteArrayOutputStream()

    try {
        def result = exec {
            workingDir = workingDirectory
            standardOutput = outputStream
            errorOutput = errorStream
            env.forEach { key, value ->
                environment key, value
            }
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine("cmd", "/c", *command)
            } else {
                commandLine(*command)
            }
        }


        if (result.exitValue != 0) {
            logger.error(errorStream.toString("UTF-8"))
            result.rethrowFailure()
        } else {
            logger.info(outputStream.toString("UTF-8"))
        }
    } catch (Exception e) {
        def errorStreamContent = errorStream.toString("UTF-8")
        def standardStreamContent = outputStream.toString("UTF-8")
        logger.error("Exception raised during command execution," +
                " stderr=${errorStreamContent}, stdout=${standardStreamContent}")
        throw e
    }
}
